# Maintainer: Vladimir Fedorov <fedotru gmail com>

pkgname=qt5-webengine-with-codecs
pkgdesc='Qt5 WebEngine with proprietary codecs'
pkgver=5.5.1
pkgrel=1
arch=('any')
url='http://www.qt.io/developers'
license=('LGPL')
makedepends=()
depends=(qt5-base git python2 libxcomposite libxrandr qt5-webchannel libxcursor libpulse nss gperf)
groups=('qt' 'qt5')
provides=('qt5-webengine')
conflicts=('qt5-webengine')

source=(
    git://code.qt.io/qt/qtwebengine.git#tag=v5.5.1
    git://code.qt.io/qt/qtwebengine-chromium.git
)
md5sums=(SKIP SKIP)

prepare() {
    # Use python2 for Python 2.x
    find . -name '*.py' -exec sed -i \
        's|#![ ]*/usr/bin/python$|&2|;s|#![ ]*/usr/bin/env python$|&2|' {} +

    # in qtwebengine there are still a lot of relative calls which need a workaround
    mkdir -p "${srcdir}"/python2-path
    ln -fs /usr/bin/python2 "${srcdir}"/python2-path/python

    #prepare git repos
    cd "$srcdir/qtwebengine"
    git submodule sync
    git submodule init
    git config submodule.src/3rdparty.url "$srcdir/qtwebengine-chromium"
    git submodule update
}

build() {
    # python2 workaround
    export PATH="${srcdir}/python2-path:$PATH"

    cd "$srcdir/qtwebengine"
    qmake WEBENGINE_CONFIG+=proprietary_codecs -r
    make
}

package() {
    cd "$srcdir/qtwebengine"

    make INSTALL_ROOT="${pkgdir}" install
}
